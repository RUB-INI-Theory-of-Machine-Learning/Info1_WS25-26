include "Assets";
include "Tetromino";
include "Playfield";
include "Renderer";
include "TetrominoPool";

# Initialize the necessary classes
var playfield = Playfield(10,20);
var pool = TetrominoPool();
var tetromino = Tetromino(pool.get());
var renderer = Renderer();


var previous_time = localtime();

var level = 0;
var lines = 0;
var points = 0;

var gravity = level_gravity[level];

setEventHandler("timer", function(event){
	if (localtime() - previous_time >= gravity) then {
		previous_time = localtime();
	
		# render the state so we can see changes
		renderer.render(playfield, tetromino, Tetromino(pool.next), points, level, lines);
	
	  	# change the y position of the block
		tetromino.drop();
		
		# check for collision, if true then undrop tetromino, insert and get new
		if playfield.check_collision(tetromino) then {
			tetromino.undo();
			if tetromino.row == 0 then quitEventMode();
			
			playfield.insert_tetromino(tetromino);
			var cleared_rows = playfield.remove_full_rows(tetromino.row);
			points +=  base_score_values[cleared_rows] * (level+1);
			lines += cleared_rows;
			level = lines//10;
			gravity = level_gravity[level];
			
			tetromino = Tetromino(pool.get());
		}
	}
});

setEventHandler("canvas.keydown", function(event){
	tetromino.save_state();
	if event.key == "6" then tetromino.move_right();
	if event.key == "4" then tetromino.move_left();
	if event.key == "5" then tetromino.rotate_clockwise();
	if event.key == "8" then tetromino.rotate_counter_clockwise();
	if event.key == " " then {
		gravity = 50;
		return;
	}
	
	if playfield.check_collision(tetromino) then {
		tetromino.undo();
	}
	else {
		# render the state so we can see changes
		var before = time();
		renderer.render(playfield, tetromino, Tetromino(pool.next), points, level, lines);
		print(time() - before);
	}

});

setEventHandler("canvas.keyup", function(event){
	if event.key == " " then gravity = level_gravity[level];
});

enterEventMode();